# Maintainer: Denys Popov <besisland@besisland.name>
pkgname=rukbi
pkgver=3.2
pkgrel=1
pkgdesc="Alternative Ukrainian and Russian keyboard layouts with miscellaneous useful typographic characters, including Ilya Birmanâ€™s layout"
url="http://besisland.name/rukbi"
arch=("any")
license=("GPLv3")
depends=("xkeyboard-config" "python")
source=("http://rukbi.googlecode.com/files/$pkgname-$pkgver.tar.bz2")
md5sums=("0cc0998145ba11fe63f6c57501df1d04")
sha1sums=("4c094bcfa88d29ef3b26606376771c91ae1b99fd")
sha256sums=("de486508ec3168de0a2a0805447028eb703157fa056b1b7ff4f903a448bdf998")
install=("arch-install")

check() {
    cd "$srcdir"
    test/test
}

package() {
    local xkbdir="$pkgdir/usr/share/X11/xkb"
    cd "$srcdir"

    install -d $xkbdir/symbols $xkbdir/rules
    install -m 0644 symbols/* $xkbdir/symbols/

    install -d $xkbdir/rukbi/install $xkbdir/rukbi/symbols $xkbdir/rukbi/rules
    install -m 0755 install/* $xkbdir/rukbi/install/
    install -m 0644 symbols/* $xkbdir/rukbi/symbols/
    install -m 0644 rules/* $xkbdir/rukbi/rules/
}
